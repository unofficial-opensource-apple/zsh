#compdef python

# Python 2.2.2

_arguments -s -S \
  '(1 -)-c+[program passed in as string (terminates option list)]:python command:' \
  '-d[debug output from parser (also PYTHONDEBUG=x)]' \
  '-E[ignore environment variables (such as PYTHONPATH)]' \
  '(1 * -)-h[print this help message and exit]' \
  '-i[inspect interactively after running script (also PYTHONINSPECT=x)]' \
  '-O[optimize generated bytecode (a tad; also PYTHONOPTIMIZE=x)]' \
  '-OO[remove doc-strings in addition to the -O optimizations]' \
  '-Q+[division options: -Qold (default), -Qwarn, -Qwarnall, -Qnew]:division option:(old warn warnall new)' \
  "-S[don't imply 'import site' on initialization]" \
  '-t[issue warnings about inconsistent tab usage]' \
  '-tt[issue errors about inconsistent tab usage]' \
  '-u[unbuffered binary stdout and stderr (also PYTHONUNBUFFERED=x)]' \
  '-v[verbose (trace import statements) (also PYTHONVERBOSE=x)]' \
  '(1 * -)-V[print the Python version number and exit]' \
  '-W+[warning control (arg is action:message:category:module:lineno)]:warning filter:(default always ignore module once error)' \
  '-x[skip first line of source, allowing use of non-Unix forms of #!cmd]' \
  '(1 -)-[program read from stdin (default; interactive mode if a tty)]' \
  '(-)1:script file:_files -g \*.py\(\|c\|o\)' \
  '*:script argument:_files' && return

return 1
